# ============================================================================
# .zshrc â€” Dev Environment
# Managed by chezmoi | Theme: Catppuccin Mocha | Prompt: Starship
# ============================================================================

# --- Lazy completion system (must be before cache which calls compdef) ---
autoload -Uz compinit
typeset -a _deferred_compdefs
compdef() { _deferred_compdefs+=("$*") }
_lazy_compinit() {
  unfunction _lazy_compinit
  compinit -C
  for def in "${_deferred_compdefs[@]}"; do eval "compdef $def"; done
  unset _deferred_compdefs
  zle -N complete-word
  zle complete-word
}
zle -N complete-word _lazy_compinit

# --- Cached tool initialization (regenerate: rm ~/.zsh_cache && exec zsh) ---
if [[ -f ~/.zsh_cache ]]; then
  source ~/.zsh_cache
else
  eval "$(/opt/homebrew/bin/brew shellenv)"
  {
    echo 'export HOMEBREW_PREFIX="/opt/homebrew"'
    echo 'export HOMEBREW_CELLAR="/opt/homebrew/Cellar"'
    echo 'export HOMEBREW_REPOSITORY="/opt/homebrew"'
    echo 'fpath[1,0]="/opt/homebrew/share/zsh/site-functions"'
    echo 'export PATH="/opt/homebrew/bin:/opt/homebrew/sbin:${PATH}"'
    echo '[ -z "${MANPATH-}" ] || export MANPATH=":${MANPATH#:}"'
    echo 'export INFOPATH="/opt/homebrew/share/info:${INFOPATH:-}"'
    fnm env --use-on-cd --shell zsh
    uv generate-shell-completion zsh
    uvx --generate-shell-completion zsh
    starship init zsh
    zoxide init zsh
    fzf --zsh
  } > ~/.zsh_cache
  source ~/.zsh_cache
fi

# --- Plugins ---
source /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh
source /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# --- History ---
HISTFILE=~/.zsh_history
HISTSIZE=50000
SAVEHIST=50000
setopt EXTENDED_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_VERIFY
setopt SHARE_HISTORY

# --- Aliases: modern CLI tools ---
alias ls="eza"
alias ll="eza -la --git --icons"
alias la="eza -a --icons"
alias tree="eza --tree --icons"
alias cat="bat --style=plain"
alias grep="rg"
alias find="fd"
alias diff="delta"
alias lg="lazygit"
alias j="just"

# --- Aliases: git shortcuts ---
alias gs="git status"
alias gd="git diff"
alias gl="git log --oneline -20"
alias gp="git push"
alias gc="git commit"

# --- Aliases: Python (Astral) ---
alias py="python3"
alias pip="uv pip"

# --- Bun ---
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"
[ -s "$HOME/.bun/_bun" ] && source "$HOME/.bun/_bun"

# --- Go ---
export GOPATH="$HOME/go"
export PATH="$GOPATH/bin:$PATH"

# --- Default directory ---
cd ~/Developer
